# @(#) $Id$

# Include polygon fill patterns and scalebar definition.
#
include symbols.mapyrus
include scalebar.mapyrus


newpage "eps", "mapview2.eps", 70, 40
worlds 40000, 40000, 50000, 45000

let scale=Mapyrus.worlds.scale

# Import polygons of imaginary vegetation.
#
dataset "textfile", "vegetation.dat", "comment=#"
while Mapyrus.fetch.more
do
	fetch
	let nCoords = $1
	let vegCode = $2

	# Add all coordinate for this polygon to path.
	#
	fetch
	clearpath
	move $1, $2
	let i = 1
	while i < nCoords
	do
		fetch
		draw $1, $2
		let i = i + 1
	done

	# Display polygon differently, depending on its vegetation class.
	#
	if vegCode == 1
	then
		savannah
	elif vegCode == 2
	then
		wheatfield
	elif vegCode == 3
	then
		barerock
	elif vegCode == 4
	then
		marsh
	elif vegCode == 5
	then
		marsh
	elif vegCode == 6
	then
		water
	endif

	# Show outline of polygon
	#
	color "black"
	linestyle 0.1
	stroke
done

newpage "eps", "mapview2legend.eps", 40, 50
clearpath
let counter=0
while counter < Mapyrus.key.count
do
	move 2, 1 + counter * 8
	let counter=counter+1
done

font "Helvetica", 3
color "black"
legend 7

# Create a scalebar for the map.
#
newpage "eps", "mapview2scalebar.eps", 50, 15
clearpath
scalebar scale, "m"

# Draw north arrow for the map.
#
newpage "eps", "mapview2north.eps", 20, 20
clearpath
move 7, 10
arc 1, 10, 10, 7, 10
move 5, 10
draw 15, 10
move 10, 5
draw 10, 15
draw 9, 14, 11, 14, 10, 15
stroke
clearpath
move 10, 16
font "Helvetica", 3
justify "center"
label "NORTH"

