# Setup for generating a temporary PNG image file
#
let tmpPNGFile = tempname("png")
newpage "png", tmpPNGFile, 100, 100
let imageSizeInPixels = Mapyrus.page.width / Mapyrus.page.resolution.mm

# The first time this URL is accessed the area to view is not given.
# So set it to cover the whole dataset.
#
dataset "shapefile", "coastline.shp", "dbffields="
if X1 eq ""
then
  let X1 = Mapyrus.dataset.min.x
  let Y1 = Mapyrus.dataset.min.y
  let X2 = Mapyrus.dataset.max.x
  let Y2 = Mapyrus.dataset.max.y
else
  let xDiff = X2 - X1
  let yDiff = Y2 - Y1
  # Location clicked in imagemap is new center point of map.
  # Calculate this location in world coordinates.
  #
  let xCenter = X1 + (Mapyrus.imagemap.x / imageSizeInPixels) * xDiff
  let yCenter = Y2 - (Mapyrus.imagemap.y / imageSizeInPixels) * yDiff
  # Set new coordinates centered on clicked point.
  #
  let X1 = xCenter - xDiff / 2
  let Y1 = yCenter - yDiff / 2
  let X2 = xCenter + xDiff / 2
  let Y2 = yCenter + yDiff / 2
endif

# Find the world coordinates again, as they have been expanded
# to fill the square image we are drawing to.
#
worlds X1, Y1, X2, Y2
let X1 = Mapyrus.worlds.min.x
let Y1 = Mapyrus.worlds.min.y
let X2 = Mapyrus.worlds.max.x
let Y2 = Mapyrus.worlds.max.y

# Read shape file and draw it into image file.
#
while Mapyrus.fetch.more
do
  clearpath
  fetch
  addpath GEOMETRY
  stroke
done

# Send HTML page back to client containing reference to the image
# we've created with URL giving current world coordinates so
# further zooming can be calculated.
#
let url = Mapyrus.filename . '?X1=' . X1 . '&Y1=' . Y1 . \
  '&X2=' . X2 . '&Y2=' . Y2
mimetype "text/html"
print '<html>'
print '<h1>Click In Image To Re-center</h1><p>'
print '<a href="' . url . '">'
print '<img src="' . tmpPNGFile . '" ismap>'
print '</a>'
