newpage "eps", "tutorialdatasets4.eps", 160, 100, "background=lightblue"
worlds -25, 30, 35, 68

# 6-class Pink-Yellow-Green diverging scheme from http://colorbrewer2.org
let c = ["#c51b7d", "#e9a3c9", "#fde0ef", "#e6f5d0", "#a1d76a", "#4d9221"]

linestyle 0.1, "round", "round"
dataset "internal", "countries", "xmin=-25 ymin=30 xmax=35 ymax=68"
while Mapyrus.fetch.more
do
  fetch
  clearpath
  addpath GEOMETRY

  # Lookup color for country from color code number.
  #
  color c[COLORCODE]
  fill
  color "grey"
  stroke
done

begin capitaldot
  box -0.5, -0.5, 0.5, 0.5
  fill
end

color "black"
font "Helvetica", 3
dataset "internal", "capitals", "xmin=-25 ymin=30 xmax=35 ymax=68"
while Mapyrus.fetch.more
do
  fetch
  clearpath
  addpath GEOMETRY
  capitaldot
  label " " . CAPITAL
done
