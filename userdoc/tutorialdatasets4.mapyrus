newpage "eps", "tutorialdatasets4.eps", 160, 100, "background=lightblue"
worlds -25, 30, 35, 68

let c = ["yellow3", "orange3", "yellow", "gold", "pastelbrown", "yellowgreen"]

linestyle 0.1, "round", "round"
dataset "internal", "countries", "xmin=-25 ymin=30 xmax=35 ymax=68"
while Mapyrus.fetch.more
do
  fetch
  clearpath
  addpath GEOMETRY

  # Lookup color for country from color code number.
  #
  color c[COLORCODE]
  fill
  color "grey"
  stroke
done

begin capitaldot
  box -0.5, -0.5, 0.5, 0.5
  fill
end

color "black"
font "Helvetica", 3
dataset "internal", "capitals", "xmin=-25 ymin=30 xmax=35 ymax=68"
while Mapyrus.fetch.more
do
  fetch
  clearpath
  addpath GEOMETRY
  capitaldot
  label " " . CAPITAL
done
