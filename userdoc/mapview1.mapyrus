# @(#) $Id$
include symbols.mapyrus

newpage "eps", "mapview1.eps", 80, 80, "isolatinfonts=Helvetica-Bold background=white"
clearpath

# Import coastline of Australia.
#
dataset "shapefile", "coastline.shp", ""
worlds -3000000, 5500000, 2600000, 8500000

while Mapyrus.fetch.more
do
	fetch
	addpath GEOMETRY
done

# Show seaward side of coastline with thick blue line.
#
begin outerCoastline
	clip "outside"
	linestyle 6, "round", "round"
	color "rgb", 0.2, 0.8, 1
	stroke
end

# Draw coastline twice for emphasis.
#
outerCoastline
linestyle 0.1, "round", "round"
color "black"
stroke
clearpath


# Import temperature for major Australian cities.
# Show histograms of temperature for each city.
#
dataset "textfile", "aust_temperatures.dat", ""
while Mapyrus.fetch.more
do
	fetch
	let city = $0

	clearpath
	fetch
	move $1, $2

	fetch

	let jan = $1
	let feb = $2
	let mar = $3
	let apr = $4
	let may = $5
	let jun = $6
	let jul = $7
	let aug = $8
	let sep = $9
	let oct = $10
	let nov = $11
	let dec = $12

	fetch
	temperatureHistogram city, jan, feb, mar, apr, may, jun, jul, aug, sep, oct, nov, dec, $1, $2, $3, $4, $5, $6, $7, $8, $9, $10, $11, $12
done

# Clear awat any legend entries so that they do not appear in any other
# file that is being executed immediately after this one.
#
worlds 0, 0, Mapyrus.page.width, Mapyrus.page.height
clearpath
move -99, -99
move -99, -99
move -99, -99
legend 1
