# @(#) $Id$

# Draw label in black, with white highlight so that it is easier to read.
#
begin highlightlabel name
	font "Helvetica-Bold", 3
	color "white"
	clearpath
	move -Mapyrus.page.resolution.mm, 0
	move Mapyrus.page.resolution.mm, 0
	move 0, -Mapyrus.page.resolution.mm
	move 0, Mapyrus.page.resolution.mm
	label name

	clearpath
	move 0, 0
	color "black"
	label name
end

# Draw a cube, viewed from the side and above.
#
begin box3d
	linestyle 0.1

	# Draw top side of box
	#
	move -1, 1
	draw -2.73, 0, -0.73, 0, 1, 1, -1, 1

	# Draw right side of box
	#
	move 1, 1
	draw 1, -1, -0.73, -2, -0.73, 0

	# Draw front side of box
	#
	box -2.73, -2, -0.73, 0

	color "yellow"
	fill
	color "black"
	stroke
end

begin warehouse name, inventory

	# Draw stacks of boxes showing inventory level at warehouse.
	#
	let x = 0
	let y = 0
	while inventory > 0
	do
		move x * 2, y
		let inventory = inventory - 1
		let x = x + 1
		if x == 5
		then
			let x = 0
			let y = y + 2
		endif
	done
	box3d

	# Show warehouse name under boxes.
	#
	clearpath
	move -4, -5
	highlightlabel name
end

newpage "eps", "mapview4.eps", 60, 60, 96

# Use light background.
#
color "rgb", 1, 0.9, 0.9
move 0, 0
draw 0, Mapyrus.page.height, Mapyrus.page.width, Mapyrus.page.height, Mapyrus.page.width, 0
fill

# Import freight rail line network in Munich
#
clearpath
dataset "textfile", "muc_freight.dat", "comment=#"
worlds 11, 48, 11.5, 48.5
import
while Mapyrus.import.more
do
	fetch
	let nCoords = $1

	# Add all coordinates for this line to path.
	#
	fetch
	move $1, $2

	let i = 1
	while i < nCoords
	do
		fetch
		draw $1, $2
		let i = i + 1
	done
done

color "#000000"
linestyle 0.8
stroke
color "#aaaaaa"
linestyle 0.8, 'butt', 'bevel', 0, 2, 2
stroke

# Display symbols showing inventory levels at warehouses.
# In a real application, this information would be read from a database.
#
clearpath
move 11.115, 48.251
warehouse "Pasing", 2
clearpath
move 11.188, 48.376
warehouse "Harthof", 19
clearpath
move 11.357, 48.298
warehouse "Feldkirchen", 7

# Add key at bottom left of page showing how many units one box represents.
#
worlds 0, 0, Mapyrus.page.width, Mapyrus.page.height
clearpath
move 7, 7
warehouse "represents 1000 units", 1
