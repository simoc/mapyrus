# Parse variables passed from HTML form, using reasonable defaults if
# they are out of range.
#
maxZoomlevel=5
if zoomlevel eq '' or zoomlevel < 0
then
	zoomlevel=0
elif zoomlevel > maxZoomlevel
then
	zoomlevel=maxZoomlevel
endif

if labels ne 'on' and labels ne 'off'
then
	labels='off'
endif

# Create temporary PNG file containing map display for this HTTP request.
#
imageFile = tempname("PNG")
newpage "png", imageFile, 100, 100, 96

# Display part of dataset, zooming in on centre based on zoom level.
#
dataset "shapefile", "/tmp/rds_l", "dbffields="
x1 = Mapyrus.dataset.min.x + Mapyrus.dataset.width * (zoomlevel / (maxZoomlevel * 2 + 1))
y1 = Mapyrus.dataset.min.y + Mapyrus.dataset.height * (zoomlevel / (maxZoomlevel * 2 + 1))
x2 = Mapyrus.dataset.max.x - Mapyrus.dataset.width * (zoomlevel / (maxZoomlevel * 2 + 1))
y2 = Mapyrus.dataset.max.y - Mapyrus.dataset.height * (zoomlevel / (maxZoomlevel * 2 + 1))

worlds x1, y1, x2, y2
import
color "red"
while Mapyrus.import.moreRecords
do
	clearpath
	fetch
	addpath GEOMETRY
	stroke
done
recordCount = Mapyrus.import.count

if labels eq 'on'
then
	# Display point map containing labels.
	#
	dataset "shapefile", "/tmp/txt_p", "dbffields=COMMENT"
	import
	color "black"
	font "Helvetica", "plain", 3
	while Mapyrus.import.moreRecords
	do
		clearpath
		fetch
		addpath GEOMETRY
		label COMMENT
	done
	recordCount = recordCount + Mapyrus.import.count
endif

print '&lt;html>'
print '&lt;img src="' . imageFile . '">'

# Generate HTML form offering options for changing map display,
# giving current values as defaults.
#
print '&lt;form method=get action="' . Mapyrus.filename . '">'
print 'Zoom level'
print '&lt;select name=zoomlevel size=1>'
i = 0
while i <= maxZoomlevel
do
	print '&lt;option value=' . i
	if i == zoomlevel
	then
		# Select current zoom factor in pop-up list.
		#
		print ' selected'
	endif

	print '>' . i
	i=i+1
done
print '&lt;/select>'

# Add toggle button for labels on/off.
#
print '&nbsp;&lt;input type=checkbox name=labels value=on'
if labels eq 'on'
then
	print ' checked'
endif
print '> Display Labels'
print '&nbsp;&lt;input type="submit" name="submit" value="Redisplay">'
print '&lt;/form>'

print '&lt;br>Record count ' . recordCount

