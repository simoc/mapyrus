# Display requested area of file roads.shp in a PNG file.
#
imageFile = tempname("PNG")
newpage "png", imageFile, 100, 100, 96

# Parse variables passed
#
maxZoomlevel=5
if zoomlevel eq '' or zoomlevel < 0
then
	zoomlevel=0
elif zoomlevel > maxZoomlevel
then
	zoomlevel=maxZoomlevel
endif

if labels ne 'on' and labels ne 'off'
then
	labels='off'
endif

dataset "shapefile", "/tmp/rds_l", "dbffields="
x1 = Mapyrus.dataset.min.x + Mapyrus.dataset.width * (zoomlevel / (maxZoomlevel * 2 + 1))
y1 = Mapyrus.dataset.min.y + Mapyrus.dataset.height * (zoomlevel / (maxZoomlevel * 2 + 1))
x2 = Mapyrus.dataset.max.x - Mapyrus.dataset.width * (zoomlevel / (maxZoomlevel * 2 + 1))
y2 = Mapyrus.dataset.max.y - Mapyrus.dataset.height * (zoomlevel / (maxZoomlevel * 2 + 1))

worlds x1, y1, x2, y2, "degrees"
import
color "red"
while Mapyrus.import.moreRecords
do
	clearpath
	fetch
	addpath GEOMETRY
	stroke
done
recordCount = Mapyrus.import.count

if labels eq 'on'
then
	dataset "shapefile", "/tmp/txt_p", "dbffields=COMMENT"
	import
	color "black"
	font "Helvetica", "plain", 3
	while Mapyrus.import.moreRecords
	do
		clearpath
		fetch
		addpath GEOMETRY
		label COMMENT
	done
endif

print '<html>'
print '<img src="' . imageFile . '">'
print '<p>'

print '<form method=get action="' . Mapyrus.filename . '">'
print 'Zoom level'
print '<select name=zoomlevel size=1>'
i = 0
while i <= maxZoomlevel
do
	print '<option value=' . i
	if i == zoomlevel
	then
		print ' selected'
	endif

	print '>' . i
	i=i+1
done
print '</select>'

print '&nbsp;<input type=checkbox name=labels value=on'
if labels eq 'on'
then
	print ' checked'
endif
print '> Display Labels'
print '&nbsp;<input type="submit" name="submit" value="Redisplay">'
print '</form>'

print '<br>Record count ' . recordCount

